// =====================================================================
// SCRIPT NAME : checkFormSubmitted
// PATH        : Master-Form-Commercial-FitUp > Created > Load of the form
// TRIGGER     : On Load
// PURPOSE     :
//   - Validate form access using form_id
//   - Prevent duplicate form submissions
//   - Stop user if response already exists
// =====================================================================

formId = input.form_id;
primary = "<style> .zc-live-primary-btn, .zc-live-secondary-btn { display:none !important; } </style>";
if(formId == null || formId.trim() == "")
{
	alert "Invalid form access.";
	return;
}
// set hidden field
// input.Form_Id = formId;
// check if response already exists
// existingResponse = Master_Form[Form_Id == formId];
existingResponse = Master_Form_Commercial_FitUp[form_id == formId];
if(existingResponse.count() > 0)
{
	alert "Form already submitted.";
	show plain;
	return;
}



// =====================================================================
// SCRIPT NAME : validateFormSubmission
// PATH        : Master-Form-Commercial-FitUp > Created or Edited
//               > Validations on form submission
// TRIGGER     : On Form Submission (Validation)
// PURPOSE     :
//   - Prevent duplicate submissions (server-side)
//   - Validate required address field
//   - Validate Email / Phone based on contact type
//   - Normalize and format phone number
//   - Validate Canadian postal code
// =====================================================================

formId = input.form_id;
existingResponse = Master_Form_Commercial_FitUp[form_id == formId];
if(existingResponse.count() > 0)
{
	alert "Form already submitted.";
	cancel submit;
}
if(input.Service_Address.address_line_1 == "" || input.Service_Address.address_line_1 == null)
{
	alert "Address Line 1 is required.";
	cancel submit;
}
mode = input.select_contact_type;
postal = input.Service_Address.postal_Code.trim().toUpperCase();
phone = input.Phone_Number1;
if(mode == "Email")
{
	if(input.Email_Address == "")
	{
		alert "Email is empty";
		cancel submit;
	}
}
else if(mode == "Phone")
{
	if(input.Phone_Number1 == "")
	{
		alert "Phone Number is empty";
		cancel submit;
	}
	phone = input.Phone_Number1;
	cleanPhone = phone.replaceAll("[^0-9]","");
	if(cleanPhone.length() == 11 && cleanPhone.startsWith("1"))
	{
		cleanPhone = cleanPhone.subString(1,11);
	}
	if(cleanPhone.length() != 10)
	{
		alert "Please enter a valid 10-digit phone number (e.g. 506-234-5678)";
		cancel submit;
	}
	input.Phone_Number1 =
		cleanPhone.subString(0,3) + "-" +
		cleanPhone.subString(3,6) + "-" +
		cleanPhone.subString(6,10);
}
else
{
	if(input.Email_Address == "" || input.Phone_Number1 == "")
	{
		alert "Email and Phone are empty";
		cancel submit;
	}
}
pattern = "[A-Z][0-9][A-Z][ ]?[0-9][A-Z][0-9]";
if(postal != "")
{
	if(!postal.matches(pattern))
	{
		alert "Please enter a valid Canadian postal code (e.g. A1A 1A1)";
		cancel submit;
	}
}



// =====================================================================
// SCRIPT NAME : phon
// PATH        : Master-Form-Commercial-FitUp > Created or Edited
//               > Validations on form submission
// TRIGGER     : On Form Submission (Validation)
// PURPOSE     :
//   - Enforce phone number when contact type is Phone or Both
//   - Clean and normalize phone digits
//   - Format phone as (XXX) XXX-XXXX
// =====================================================================

mode = input.select_contact_type;
if(mode == "Phone" || mode == "Both" && input.Phone_Number1 == null || input.Phone_Number1.trim() == "")
{
	alert "Phone number is required";
	cancel submit;
}
phone = input.Phone_Number1.toString();
digits = phone.replaceAll("[^0-9]","");
if(digits.length() > 10)
{
	digits = digits.right(10);
}
if(digits.length() != 10)
{
	alert "Enter a valid 10-digit phone number";
	cancel submit;
}
formattedPhone =
	"(" + digits.subString(0,3) + ") " +
	digits.subString(3,6) + "-" +
	digits.subString(6,10);
input.Phone_Number1 = formattedPhone;



// =====================================================================
// SCRIPT NAME : storeFilesOnAttachments
// PATH        : Master-Form-Commercial-FitUp > Created or Edited
//               > Successful form submission
// TRIGGER     : On Success
// PURPOSE     :
//   - Upload form attachments to CRM Lead
//   - Notify Lead owner by email
//   - Reset follow-up fields based on Lead status
// =====================================================================

leadId = input.lead_id;
if(leadId != null && input.Upload_Files != null)
{
	for each singleFile in input.Upload_Files
	{
		fileToUpload = singleFile;
		if(singleFile.toString().contains("http"))
		{
			fileToUpload = invokeurl
			[
				url :singleFile
				type :GET
			];
		}
		zoho.crm.attachFile("Leads",leadId.toLong(),fileToUpload);
	}
	leadRec = zoho.crm.getRecordById("Leads",leadId.toLong());
	if(leadRec == null)
	{
		return;
	}
	ownerMap = leadRec.get("Owner");
	managerEmail = ownerMap.get("email");
	if(managerEmail == null || managerEmail == "")
	{
		return;
	}
	crmUrl = "https://crmsandbox.zohocloud.ca/crm/sandboxcreator/tab/Leads/" + leadId;
	subject = "Questionnaire Submitted â€“ Action Required";
	message =
		"A client has submitted the questionnaire.\n\n" +
		"Lead Name: " + leadRec.get("Full_Name") + "\n" +
		"Lead Status: " + leadRec.get("Lead_Status") + "\n" +
		"View Lead: " + crmUrl;
	sendmail
	[
		from :zoho.adminuserid
		to :managerEmail
		subject :subject
		message :message
	]
	currentStatus = leadRec.get("Lead_Status");
	updateMap = Map();
	doUpdate = false;
	if(currentStatus == "Questionnaire Sent")
	{
		updateMap.put("Follow_Up_1",null);
		updateMap.put("Follow_Up_2",null);
		updateMap.put("Follow_Up_3",null);
		doUpdate = true;
	}
	else if(currentStatus == "Additional Info Required")
	{
		updateMap.put("Follow_Up_1",null);
		updateMap.put("Follow_Up_2",null);
		updateMap.put("Follow_Up_3",null);
		doUpdate = true;
	}
	if(doUpdate)
	{
		val = zoho.crm.updateRecord("Leads",leadId.toLong(),updateMap);
		info val;
	}
}
